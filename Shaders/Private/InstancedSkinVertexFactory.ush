#include "/Engine/Public/Platform.ush"

#ifndef INSTANCED_SKIN_LIMIT_2BONE_INFLUENCES
#define INSTANCED_SKIN_LIMIT_2BONE_INFLUENCES 0 // default is 4 bones
#endif

#ifndef INSTANCED_SKIN_DISABLE_ANIMATION_BLEND
#define INSTANCED_SKIN_DISABLE_ANIMATION_BLEND 0 // default is enable
#endif

#ifndef INSTANCED_SKIN_DISABLE_FRAME_LERP
#define INSTANCED_SKIN_DISABLE_FRAME_LERP 0 // default is enable
#endif

struct FVertexFactoryInput
{
	float4	Position		: ATTRIBUTE0;
	// 0..1
	half3	TangentX		: ATTRIBUTE1;
	// 0..1
	// TangentZ.w contains sign of tangent basis determinant
	half4	TangentZ		: ATTRIBUTE2;	

#if FEATURE_LEVEL >= FEATURE_LEVEL_ES3_1 || COMPILER_METAL || COMPILER_VULKAN
	uint4	BlendIndices	: ATTRIBUTE3;
#else
	// Continue using int for SM3, compatibility of uint is unknown across SM3 platforms
	int4	BlendIndices	: ATTRIBUTE3;
#endif
	float4	BlendWeights	: ATTRIBUTE4;

#if NUM_MATERIAL_TEXCOORDS_VERTEX
	// If this changes make sure to update LocalVertexFactory.usf
	float2	TexCoords[NUM_MATERIAL_TEXCOORDS_VERTEX] : ATTRIBUTE5;

	#if NUM_MATERIAL_TEXCOORDS_VERTEX > 4
		#error Too many texture coordinate sets defined on GPUSkin vertex input. Max: 4.
	#endif
#endif

	/** Per vertex color */
	float4 Color : ATTRIBUTE13;
	
#if FEATURE_LEVEL >= FEATURE_LEVEL_ES3_1
	uint InstanceId	: SV_InstanceID;
#endif
};

struct FVertexFactoryInterpolantsVSToPS
{
	TANGENTTOWORLD_INTERPOLATOR_BLOCK

#if INTERPOLATE_VERTEX_COLOR
	float4 Color : COLOR0;
#endif

#if NUM_TEX_COORD_INTERPOLATORS
	float2	TexCoords[NUM_TEX_COORD_INTERPOLATORS]	: TEXCOORD0;
#endif

#if INSTANCED_STEREO
	nointerpolation uint EyeIndex : PACKED_EYE_INDEX;
#endif
};

struct FVertexFactoryInterpolantsVSToDS
{
	FVertexFactoryInterpolantsVSToPS InterpolantsVSToPS;
};

/** Converts from vertex factory specific interpolants to a FMaterialPixelParameters, which is used by material inputs. */
FMaterialPixelParameters GetMaterialPixelParameters(FVertexFactoryInterpolantsVSToPS Interpolants, float4 SvPosition)
{
	// GetMaterialPixelParameters is responsible for fully initializing the result
	FMaterialPixelParameters Result = MakeInitializedMaterialPixelParameters();

#if NUM_TEX_COORD_INTERPOLATORS	
	UNROLL
	for(int CoordinateIndex = 0;CoordinateIndex < NUM_TEX_COORD_INTERPOLATORS;CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = Interpolants.TexCoords[CoordinateIndex];
	}
#endif

	half3 TangentToWorld0 = Interpolants.TangentToWorld0.xyz;
	half4 TangentToWorld2 = Interpolants.TangentToWorld2;
	Result.TangentToWorld = AssembleTangentToWorld( TangentToWorld0, TangentToWorld2 );
#if USE_WORLDVERTEXNORMAL_CENTER_INTERPOLATION
	Result.WorldVertexNormal_Center = Interpolants.TangentToWorld2_Center.xyz;
#endif
	Result.UnMirrored = TangentToWorld2.w;
#if INTERPOLATE_VERTEX_COLOR
	Result.VertexColor = Interpolants.Color;
#else
	Result.VertexColor = 0;
#endif
	Result.TwoSidedSign = 1;
	return Result;
}

#define FBoneMatrix float4x4

STRONG_TYPE Buffer<uint> BoneMap;
STRONG_TYPE Buffer<float4> RefBasesInvMatrix;
STRONG_TYPE Buffer<float4> BoneMatrices;
STRONG_TYPE Buffer<float4> InstanceMatrices;
STRONG_TYPE Buffer<uint> InstanceAnimations;

FBoneMatrix GetRefBasesInvMatrixFromBuffer(int BoneId)
{
	int Offset = BoneId * 3;
	return FBoneMatrix(RefBasesInvMatrix[Offset], RefBasesInvMatrix[Offset + 1], RefBasesInvMatrix[Offset + 2], float4(0, 0, 0, 1));
}

float4x4 GetInstanceMatrix(int InstanceId)
{
	float4 A = InstanceMatrices[InstanceId * 4];
	float4 B = InstanceMatrices[InstanceId * 4 + 1];
	float4 C = InstanceMatrices[InstanceId * 4 + 2];
	float4 D = InstanceMatrices[InstanceId * 4 + 3];
	return float4x4(A, B, C, D);
}

FBoneMatrix GetBoneMatrixFromBuffer(int _Offset)
{
	int Offset = _Offset * 3;
	return FBoneMatrix(BoneMatrices[Offset], BoneMatrices[Offset + 1], BoneMatrices[Offset + 2], float4(0, 0, 0, 1));
}

FBoneMatrix GetBoneMatrixByInstanceAnimation(int InstanceId, int Layer, int BoneId)
{
	int Index = InstanceId * 8 + Layer * 4;
	int PrevFrame = InstanceAnimations[Index + 0];
	
#if !INSTANCED_SKIN_DISABLE_ANIMATION_BLEND
	float BlendWeight = InstanceAnimations[Index + 3] * 0.001f;
#else
	float BlendWeight = 1;
#endif

	FBoneMatrix InvMatrix = GetRefBasesInvMatrixFromBuffer(BoneId);
	
	FBoneMatrix Prev = mul(GetBoneMatrixFromBuffer(PrevFrame + BoneMap[BoneId]), InvMatrix);
	
#if !INSTANCED_SKIN_DISABLE_FRAME_LERP
	int NextFrame = InstanceAnimations[Index + 1];
	float FrameLerp = InstanceAnimations[Index + 2] * 0.001f;
	FBoneMatrix Next = mul(GetBoneMatrixFromBuffer(NextFrame + BoneMap[BoneId]), InvMatrix);
	return lerp(Prev, Next, FrameLerp) * BlendWeight;
#else
	return Prev * BlendWeight;
#endif
}

FBoneMatrix GetBoneMatrix(int InstanceId, int BoneId)
{
	FBoneMatrix M = GetBoneMatrixByInstanceAnimation(InstanceId, 0, BoneId);
#if !INSTANCED_SKIN_DISABLE_ANIMATION_BLEND
	M += GetBoneMatrixByInstanceAnimation(InstanceId, 1, BoneId);
#endif
	return M;
}

// Cache data to avoid multiple calculation 
struct FVertexFactoryIntermediates
{
	// Blend Matrix (used for position/tangents)
	FBoneMatrix BlendMatrix;
	
	// Instance Matrix (used for position/tangents)
	float4x4 InstanceMatrix;

	// Unpacked position
	float3 UnpackedPosition;
	
	// Tangent Basis
	float3x3 TangentToLocal; 

	// Vertex Color
	float4 Color;
};

MaterialFloat3x3 InstanceGetLocalToWorld3x3(FVertexFactoryIntermediates Intermediates)
{
	return (MaterialFloat3x3)Intermediates.InstanceMatrix;
}

/** Converts from vertex factory specific input to a FMaterialVertexParameters, which is used by vertex shader material inputs. */
FMaterialVertexParameters GetMaterialVertexParameters(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 WorldPosition, float3x3 TangentToLocal)
{
	FMaterialVertexParameters Result = (FMaterialVertexParameters)0;
	Result.WorldPosition = WorldPosition;
	Result.VertexColor = Intermediates.Color;
	Result.TangentToWorld = mul(TangentToLocal, InstanceGetLocalToWorld3x3(Intermediates));
	Result.PreSkinnedPosition = Intermediates.UnpackedPosition.xyz;
	Result.PreSkinnedNormal = Input.TangentZ.xyz;

	// Assumes no instacing
	Result.PrevFrameLocalToWorld = Primitive.PreviousLocalToWorld;

#if NUM_MATERIAL_TEXCOORDS_VERTEX
	for(int CoordinateIndex = 0; CoordinateIndex < NUM_MATERIAL_TEXCOORDS_VERTEX; CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = Input.TexCoords[CoordinateIndex];
	}
#endif
	return Result;
}

/**
* Unpack position - uncompress xyz position to world position
*/
float3 UnpackedPosition( FVertexFactoryInput Input )
{
		return float3(Input.Position.xyz);
}

FBoneMatrix CalcBoneMatrix( FVertexFactoryInput Input )
{
#if FEATURE_LEVEL >= FEATURE_LEVEL_ES3_1
	int InstanceId = Input.InstanceId;
#else
	int InstanceId = 0;
#endif
	FBoneMatrix BoneMatrix = Input.BlendWeights.x * GetBoneMatrix(InstanceId, Input.BlendIndices.x);
	BoneMatrix += Input.BlendWeights.y * GetBoneMatrix(InstanceId, Input.BlendIndices.y);
#if !INSTANCED_SKIN_LIMIT_2BONE_INFLUENCES
	BoneMatrix += Input.BlendWeights.z * GetBoneMatrix(InstanceId, Input.BlendIndices.z);
	BoneMatrix += Input.BlendWeights.w * GetBoneMatrix(InstanceId, Input.BlendIndices.w);
#endif
	return BoneMatrix;
}

/** transform position by weighted sum of skinning matrices */
float3 SkinPosition( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
	float4 Position = float4(Intermediates.UnpackedPosition,1);

	// Note the use of mul(Matrix,Vector), bone matrices are stored transposed
	// for tighter packing.
	return mul( Intermediates.BlendMatrix, Position );
}

/** transform the tangent basis vectors */
float3x3 SkinTangents( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
	float3x3 TangentToLocal;

	// tangent
	// -1..1
	half3 LocalTangentX = Input.TangentX;
	// -1..1 .xyz:normal, .w:contains sign of tangent basis determinant (left or right handed)
	half4 LocalTangentZ = Input.TangentZ;

	// Note the use of mul(Matrix,Vector), bone matrices are stored transposed
	// for tighter packing.
	TangentToLocal[0] = mul( Intermediates.BlendMatrix, float4(LocalTangentX, 0) );
	TangentToLocal[2] = mul( Intermediates.BlendMatrix, float4(LocalTangentZ.xyz, 0) );

	// derive the new binormal by getting the cross product of the normal and tangent
	// and flip vector based on sign of tangent basis determinant
	TangentToLocal[1] = cross(TangentToLocal[2], TangentToLocal[0]) * LocalTangentZ.w;

	return TangentToLocal;
}

FVertexFactoryIntermediates GetVertexFactoryIntermediates(FVertexFactoryInput Input)
{
	FVertexFactoryIntermediates Intermediates;
	
#if FEATURE_LEVEL >= FEATURE_LEVEL_ES3_1
	int InstanceId = Input.InstanceId;
#else
	int InstanceId = 0;
#endif
	
	Intermediates.UnpackedPosition = UnpackedPosition(Input);
	Intermediates.BlendMatrix = CalcBoneMatrix( Input );
	Intermediates.InstanceMatrix = GetInstanceMatrix(InstanceId);

	// Fill TangentToLocal
	Intermediates.TangentToLocal = SkinTangents(Input, Intermediates);

	// Swizzle vertex color.
	Intermediates.Color = Input.Color FCOLOR_COMPONENT_SWIZZLE;

	return Intermediates;
}

float4 CalcWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	float3 LocalPosition = SkinPosition(Input, Intermediates);
	float4x4 LocalToWorld = Intermediates.InstanceMatrix;
	float3 RotatedPosition = LocalToWorld[0].xyz * LocalPosition.xxx + LocalToWorld[1].xyz * LocalPosition.yyy + LocalToWorld[2].xyz * LocalPosition.zzz;
	return float4(RotatedPosition + (LocalToWorld[3].xyz + ResolvedView.PreViewTranslation.xyz),1);
}

/**
* Get the 3x3 tangent basis vectors for this vertex factory
*
* @param Input - vertex input stream structure
* @return 3x3 matrix
*/
float3x3 VertexFactoryGetTangentToLocal( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return Intermediates.TangentToLocal;
}

float4 VertexFactoryGetWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return CalcWorldPosition(Input, Intermediates);
}

float4 VertexFactoryGetRasterizedWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float4 InWorldPosition)
{
	return InWorldPosition;
}

float3 VertexFactoryGetPositionForVertexLighting(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 TranslatedWorldPosition)
{
	return TranslatedWorldPosition;
}

void CalcTangentToWorld(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, out float3 TangentToWorld0, out float4 TangentToWorld2)
{
	float3x3 LocalToWorld = InstanceGetLocalToWorld3x3(Intermediates);

	// Remove scaling.
	half3 InvScale = Primitive.InvNonUniformScaleAndDeterminantSign.xyz;
	LocalToWorld[0] *= InvScale.x;
	LocalToWorld[1] *= InvScale.y;
	LocalToWorld[2] *= InvScale.z;

	float3x3 TangentToWorld = mul(Intermediates.TangentToLocal, LocalToWorld);

	TangentToWorld0 = TangentToWorld[0];
	TangentToWorld2 = float4(TangentToWorld[2], Input.TangentZ.w * Primitive.InvNonUniformScaleAndDeterminantSign.w);
}

FVertexFactoryInterpolantsVSToPS VertexFactoryGetInterpolantsVSToPS(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
{
	FVertexFactoryInterpolantsVSToPS Interpolants;

#if NUM_TEX_COORD_INTERPOLATORS
	float2 CustomizedUVs[NUM_TEX_COORD_INTERPOLATORS];
	GetMaterialCustomizedUVs(VertexParameters, CustomizedUVs);
	GetCustomInterpolators(VertexParameters, CustomizedUVs);

	UNROLL
	for (int CoordinateIndex = 0; CoordinateIndex < NUM_TEX_COORD_INTERPOLATORS; CoordinateIndex++)
	{
		Interpolants.TexCoords[CoordinateIndex] = CustomizedUVs[CoordinateIndex];
	}
#endif

	Interpolants.TangentToWorld0.w = 0;
	CalcTangentToWorld(Input, Intermediates, Interpolants.TangentToWorld0.xyz, Interpolants.TangentToWorld2);
#if USE_WORLDVERTEXNORMAL_CENTER_INTERPOLATION
	Interpolants.TangentToWorld2_Center = Interpolants.TangentToWorld2;
#endif

#if INTERPOLATE_VERTEX_COLOR
	Interpolants.Color = Intermediates.Color;
#endif

#if INSTANCED_STEREO
	Interpolants.EyeIndex = 0;
#endif

	return Interpolants;
}

// @return previous translated world position
float4 VertexFactoryGetPreviousWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	float4x4 PreviousLocalToWorldTranslated = Primitive.PreviousLocalToWorld;

	PreviousLocalToWorldTranslated[3][0] += ResolvedView.PrevPreViewTranslation.x;
	PreviousLocalToWorldTranslated[3][1] += ResolvedView.PrevPreViewTranslation.y;
	PreviousLocalToWorldTranslated[3][2] += ResolvedView.PrevPreViewTranslation.z;

	float4 PrevSkinPosInWorld = mul(float4(SkinPosition(Input, Intermediates),1), PreviousLocalToWorldTranslated);
	return PrevSkinPosInWorld;
}

float4 VertexFactoryGetTranslatedPrimitiveVolumeBounds(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}

uint VertexFactoryGetPrimitiveId(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}
